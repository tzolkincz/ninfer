#include "ops/attn_input_proj/fp8/fp8_attn_input_plan.h"
#include "ops/attn_input_proj/fp8/fp8_attn_input_output.cuh"
#include "ops/linear/fp8/fp8_template_launch.cuh"
#include "ops/linear/fp8/fp8_instances.cuh"

namespace ninfer::ops::detail {
namespace {
using Tma64x128 = Fp8A8TmaMmaSchedule<64, 128, 128, 2, 4, 2, 1>;
using Tma64x256 = Fp8A8TmaMmaSchedule<64, 256, 128, 2, 4, 2, 1>;
using Tma96x256 = Fp8A8TmaMmaSchedule<96, 256, 128, 3, 4, 2, 1>;
using Bulk      = Fp8A8TmaSplitKSchedule<Fp8A8TmaMmaSchedule<128, 256, 128, 2, 4, 2, 1>, 170, 4, 8>;

} // namespace

std::size_t fp8_attn_input_partial_capacity_bytes(std::int32_t max_tokens) {
    // The [7168,5120] shard reaches an underfilled final wave at T=289, before the parent does.
    return max_tokens > 288 ? Bulk::kPartialBytes : 0;
}

void fp8_attn_input_a8_launch(const Tensor& x, const Weight& weight, Tensor& q, Tensor& gate,
                              Tensor& key, Tensor& value, Fp8A8Workspace workspace,
                              cudaStream_t stream) {
    launch_fp8_a8_quantize(x, weight, workspace, stream);
    visit_fp8_attn_input_problem(weight.n, [&]<class Problem>() {
        const typename Problem::Output output{
            static_cast<__nv_bfloat16*>(q.data), static_cast<__nv_bfloat16*>(key.data),
            static_cast<__nv_bfloat16*>(gate.data), static_cast<__nv_bfloat16*>(value.data)};
        const auto operands = fp8_a8_operands(weight, workspace, x.ne[1]);
        const auto launch   = [&]<class Schedule>() {
            using S = Fp8ScheduleInstance<Schedule, 5120>;
            // Row tiles stay within one section at the parent and at the shard.
            static_assert((Problem::kQueryRows % S::kBlockRows) == 0);
            static_assert((Problem::kKeyRows % S::kBlockRows) == 0);
            if constexpr (S::kTmaSwizzle)
                launch_fp8_a8_tma_mma<S>(operands, output, LinearIdentityEpilogue{}, stream,
                                         workspace.partials);
            else
                launch_fp8_a8_mma<S>(operands, output, LinearIdentityEpilogue{}, stream);
        };
        if (x.ne[1] <= 32) return launch.template operator()<Fp8A8T32R32K128>();
        if (x.ne[1] <= 96) return launch.template operator()<Fp8A8T32R128K128>();
        if (x.ne[1] <= 128) return launch.template operator()<Tma64x128>();
        if (x.ne[1] <= 192) return launch.template operator()<Tma64x256>();
        // Three 96-token tiles give 168 CTAs: one almost-full wave through T=288.
        if (x.ne[1] <= 288) return launch.template operator()<Tma96x256>();
        launch.template operator()<Bulk>();
    });
}
} // namespace ninfer::ops::detail
